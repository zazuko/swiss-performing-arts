PREFIX spav: <http://vocab.performing-arts.ch/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX apf: <http://jena.apache.org/ARQ/property#>

WITH <http://example.com/sts>

DELETE {
  ?s spav:firstPerformanceDate ?date ;
     spav:r_premiereType ?premiereTypes ;
     spav:r_isLocatedAt ?venueLabel .
}
INSERT { 
  ?s spav:hasRepresentation ?performanceUri .

  ?venueUri a spav:E22_Venue ;
    rdfs:label ?venueLabel .

  ?performanceUri a spav:E10_Performance ;
    spav:premiereType ?premiereTypeUri ;
    spav:performanceDate ?date ;
    spav:isLocatedAt ?venueUri .
}
WHERE {
    
    ?s a spav:E9_PerformingArtsProduction .

    OPTIONAL { ?s spav:firstPerformanceDate ?date . }    
    
    OPTIONAL { ?s spav:r_isLocatedAt ?venueLabel . }    
    
    OPTIONAL { ?s spav:r_premiereType ?premiereTypes . }

    BIND(URI(CONCAT("http://data.performing-arts.ch/performance/", STRUUID())) AS ?performanceUri)

    BIND(URI(CONCAT("http://data.performing-arts.ch/venue/", SHA1(?venueLabel))) AS ?venueUri)

    ?premiereTypeSplit apf:strSplit (?premiereTypes ",")
    BIND(LCASE(REPLACE(?premiereTypeSplit, " ", "")) AS ?premiereTypeKey)

    VALUES (?premiereTypeKey ?premiereTypeUri) { 
      ("ea" <http://taxonomies.performing-arts.ch/premiereType/ea>)
      ("ma" <http://taxonomies.performing-arts.ch/premiereType/ma>)
      ("de" <http://taxonomies.performing-arts.ch/premiereType/de>)
      ("gs" <http://taxonomies.performing-arts.ch/premiereType/gs>)
      ("ua" <http://taxonomies.performing-arts.ch/premiereType/ua>)
      ("se" <http://taxonomies.performing-arts.ch/premiereType/se>)
      ("wa" <http://taxonomies.performing-arts.ch/premiereType/wa>)
      ("ni" <http://taxonomies.performing-arts.ch/premiereType/ni>)
      ("ee" <http://taxonomies.performing-arts.ch/premiereType/ee>)
      ("ie" <http://taxonomies.performing-arts.ch/premiereType/ie>)
      ("fe" <http://taxonomies.performing-arts.ch/premiereType/fe>)
    }
}