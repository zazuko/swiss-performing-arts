PREFIX spav: <http://vocab.performing-arts.ch/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX apf: <http://jena.apache.org/ARQ/property#>

WITH <http://example.com/sts>

DELETE {
  ?s spav:firstPerformanceDate ?date ;
     spav:premiereType ?premiereType ;
     spav:r_isLocatedAt ?venueLabel .
}
INSERT { 
  ?s spav:hasRepresentation ?performanceUri .

  ?venueUri a spav:E22_Venue ;
    rdfs:label ?venueLabel .

  ?performanceUri a spav:E10_Performance ;
    spav:premiereType ?premiereTypeUri ;
    spav:performanceDate ?date ;
    spav:isLocatedAt ?venueUri .
}
WHERE {
    
    ?s a spav:E9_PerformingArtsProduction ;
        spav:firstPerformanceDate ?date .

    OPTIONAL { ?s spav:premiereType ?premiereType . }

    OPTIONAL { ?s spav:r_isLocatedAt ?venueLabel . }    
    
    BIND(URI(CONCAT("http://data.performing-arts.ch/performance/", STRUUID())) AS ?performanceUri)

    ?premiereTypeSplit apf:strSplit (?premiereType ",")

    BIND(URI(CONCAT("http://data.performing-arts.ch/venue/", SHA1(?venueLabel))) AS ?venueUri)

    BIND(URI(CONCAT("http://taxonomies.performing-arts.ch/premiereType/", ?premiereTypeSplit)) AS ?premiereTypeUri)

}